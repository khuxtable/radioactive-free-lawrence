info
    name: "Radio Free Lawrence",
    version: "3.0",
    author: "Kathryn Huxtable, Ron Burk, David Wiley",
    date: "20 June 1982";

include "verbs.gdesc";

flags variable
    moved;          // Optional STATUS variable flag, maintained by the kernel

flags place
    beenHere;       // place flag, maintained by the kernel

flags object
    seen,           // object flag, maintained by the kernel
    cantDrop,
    cantTake;

variable playerPower;
variable emeraldCityCount;

include "objects.gdesc";

include "places.gdesc";

include "actions.gdesc";

initial {
    apport(axe, in_spencer);
    apport(bagel, krogers); // copy
    apport(banana, twenty_third);
    apport(book, stack_warned);
    apport(box, mazel);
    apport(chain, jayhawk_sunflower);
    apport(chicken_sandwich, cave); // copy
    apport(coke, In_Joes);
    apport(dagger, lot);
    apport(glove, slums);
    apport(gun, dark); // copy
    apport(hatchet, naismith); // copy
    apport(hole, slums);
    apport(newspaper, north_wescoe);
    apport(opera_glove, naismith);
    apport(pistol, o_zone); // copy
    apport(revolver, pterodactyl);
    apport(rifle, puddle);
    apport(sandwich, In_Joes);
    apport(sword, jayhawk_sunflower);
    apport(token, sunnyside_naismith);
    apport(udk, murphy); // copy
    apport(zapgun, wescoe_cafeteria);

    apport(meter, inhand);
    setflag(meter, cantDrop);
    setflag(meter, cantTake);

    playerPower = 1000;
}
initial {
    goto(ninth_mississippi);
}
repeat {
    // Handle player location description
    if (isflag(status, moved)) {
        describe(here);
        var youSee = true;
        for (var optr : here) {
            if (youSee) {
                say("");
                youSee = false;
            }
            describe(optr);
        }
    }
}
repeat {
    // Handle player input
    input();                    // Get and parse player's command
    //say("arg1 is $", arg1);
    //say("arg2 is $", arg2);
    //say("status is $", status);
    if (status < 0) {           // Can't find 2nd or 1st command word in the vocabulary
        quip("Pardon?");        // Command parse failure -- abort the main loop
    }
    if (key("quit")) {    // If the word QUIT occurs in the command, exit the game
        say("As you wish.");
        stop();
    }

    playerPower--;
    here();             // Execute code, if any, associated with this location
    arg1();             // Execute action verb, if any
    for (var optr : inhand) {
        optr();         // Execute code, if any, associated with this object
    }
    playerPower++;

    bailOut();         // Command not handled - report an error
}

    // Deliver invalid command message
proc bailOut {
    if (status == 1) {
        if (arg1 instanceof object) { // If first word is an object name
            if (!isnear(arg1)) {
                quip("There is no # here!", arg1);
            } else {
                quip("You need to say what you want to do with the #.", arg1);
            }
        } else {                    // Must be a verb
            quip("You need to say what you want to #.", arg1);
        }
    }
    quip("You can't do that!");     // Generic no can do
}

text noDirection
    """
    You can't tell directions here.
    Try Up, Down, Left, Right, Forward, Backward""";
text whatIsNorth "What is \"North?\"";
text whatIsSouth "What is \"South?\"";
text whatIsEast "What is \"East?\"";
text whatIsWest "What is \"West?\"";
text whatIsUp "What is \"Up?\"";
text whatIsDown "What is \"Down?\"";
text whatIsLeft "What is \"Left?\"";
text whatIsRight "What is \"Right?\"";
text whatIsForward "What is \"Forward?\"";
text whatIsBackward "What is \"Backward?\"";
text tryBoot
    """
    You open the file to reveal the message:
            To get out of the computer, try 'Boot'.""";
text brightFlashAnd
    """
    There's no place like home.

    There is a bright flash and ...""";
