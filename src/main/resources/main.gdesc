info
    name: "Radio Free Lawrence",
    version: "3.0",
    author: "Kathryn Huxtable, Ron Burk, David Wiley",
    date: "20 June 1982";

include "verbs.gdesc";

flags variable
    moved;          // Optional STATUS variable flag, maintained by the kernel

flags place
    been.here;      // place flag, maintained by the kernel

flags object
    seen;           // object flag, maintained by the kernel

include "objects.gdesc";

include "places.gdesc";

include "actions.gdesc";

initial {
    apport(coke, In_Joes);
    apport(sandwich, In_Joes);
}
initial {
    goto(Ninth_Mississippi);
}
repeat {
    if (isflag(status, moved)) {
        describe(here);
        var youSee = true;
        for (var optr : here) {
            if (youSee) {
                say("");
                youSee = false;
            }
            describe(optr);
        }
    }
    input();                    // Get and parse player's command
    //say("arg1 is $", arg1);
    //say("arg2 is $", arg2);
    //say("status is $", status);
    if (status < 0) {           // Can't find 2nd or 1st command word in the vocabulary
        quip("Pardon?");        // Command parse failure -- abort the main loop
    }
}
repeat {
    if (key(quit)) {    // If the word QUIT occurs in the command, exit the game
        say("As you wish.");
        stop();
    }

    here();             // Execute code, if any, associated with this location
    arg1();             // Execute action verb, if any
    for (var optr : inhand) {
        optr();         // Execute code, if any, associated with this object
    }

    bail.out();         // Command not handled - report an error
}

// Deliver invalid command message
proc bail.out {
    if (status == 1) {
        if (arg1 instanceof object) { // If first word is an object name
            if (!isnear(arg1)) {
                quip("There is no # here!", arg1);
            } else {
                quip("You need to say what you want to do with the #.", arg1);
            }
        } else {                    // Must be a verb
            quip("You need to say what you want to #.", arg1);
        }
    }
    quip("You can't do that!");     // Generic no can do
}
